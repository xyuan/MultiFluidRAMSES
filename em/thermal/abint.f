C-----------------------------------------------------------------------
C ABSORPTION PAR LE MILIEU INTERSTELLAIRE 
C COMPOSITION CHIMIQUE NORMALE, MATIERE NEUTRE
C MORRISON AND MCCAMMON '83 APJ 270,119   
C VALABLE DE 30 EV (He) A 10 KEV
C PHOTOELECTRIQUE SEULEMENT, PAS DE DIFFUSION COMPTON
C VERSION SIMPLE PRECISION
C PARAMETRES D'ENTREE:
C - NHINT: COLONNE DENSITE D'HYDROGENE EN CM-2
C - NFC: NOMBRE DE CANAUX EN ENERGIE
C - ED(NFC+1): ENERGIES LIMITES DES CANAUX EN EV
C PARAMETRES DE SORTIE:
C - ABINT(NFC): COEFFICIENTS D'ABSORPTION
C-----------------------------------------------------------------------
      SUBROUTINE INTER(NHINT,ED,ABINT,NFC)
      INCLUDE 'include.f'
      PARAMETER(JDIM=14)
      REAL*4 NHINT,MUINT
      DIMENSION MUINT(NENERG),ABINT(NENERG),ED(NENERG+1)
      DIMENSION EDGE(JDIM),CTE1(JDIM),CTE2(JDIM),CTE3(JDIM)
C  DONNEES POUR L ABSORPTION INTERSTELLAIRE
C DISCONTINUITES CORRESPONDANT A L'IONISATION DE
C  - , C  , N  , O  ,Fe L, Ne , Mg , Si , S  , Ar , Ca ,Fe K, Ni , -
C AVEC LES ABONDANCES (EN LOG10, H A 12.00)
C  - ,8.65,7.96,8.87,7.52,8.14,7.60,7.57,7.28,6.58,6.35,7.52,6.26, -
      DATA EDGE/100.,284.,400.,532.,707.,867.,1303.,1840.,2471.,3210.,
     &4038.,7111.,8331.,1.E20/
C COEFFICIENTS POLYNOMIAUX
      DATA CTE1/17.3,34.6,78.1,71.4,95.5,308.9,120.6,141.3,202.7,
     &342.7,352.2,433.9,629.0,701.2/
      DATA CTE2/608.1,267.9,18.8,66.8,145.8,-380.6,169.3,146.8,104.7,
     &18.7,18.7,-2.4,30.9,25.2/
      DATA CTE3/-2125.,-476.1,4.3,-51.4,-61.1,294.,-47.7,-31.5,-17.,
     &0.,0.,0.75,0.,0./
C CALCUL DU COEFF ABSORP INTERSTELLAIRE EN 10(-24) CM2/AT.H.
      J = 1
      DO WHILE (ED(1) .GE. EDGE(J))
         J = J + 1
      ENDDO
      DO I = 1,NFC
         IF (ED(I+1).LE.EDGE(J) .OR. ED(I).GT.EDGE(J)) THEN
C CAS OU ON EST ENTRE DEUX DISCONTINUITES OU AU-DELA DE LA DERNIERE
            EC = 0.5 * (ED(I)+ED(I+1))
            MUINT(I) = FMUINT(EC,CTE1(J),CTE2(J),CTE3(J))
         ELSE
C CAS OU ON PASSE PAR UNE DISCONTINUITE
            XL1 = EDGE(J) - ED(I)
            CANE = ED(I+1) - ED(I)
            XL2 = CANE - XL1
            E1 = ED(I) + 0.5*XL1
            FMU1 = FMUINT(E1,CTE1(J),CTE2(J),CTE3(J))
            E2 = ED(I+1) - 0.5*XL2
            FMU2 = FMUINT(E2,CTE1(J+1),CTE2(J+1),CTE3(J+1))
            MUINT(I)= (FMU1*XL1+FMU2*XL2) / CANE
            J = J + 1
         ENDIF
      ENDDO
      DO I = 1,NFC
         U = 1.E-24 * NHINT * MUINT(I)
         U = AMIN1(120.,U)
         ABINT(I) = EXP(-U)
      ENDDO
      RETURN
      END SUBROUTINE INTER
C-----------------------------------------------------------------------
      FUNCTION FMUINT(E,C1,C2,C3)
      F=E/1000.
      FMUINT = (F**(-3))*(C1 + C2*F +C3*F*F)
      RETURN
      END FUNCTION FMUINT
